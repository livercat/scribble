var elemental_items=[
  ["hammer",
    ["aether","D",],
    ["poison","D+8","D+7"],
    ["ice","D"],
    ["default",
     "stone hammer+7","aether hammer D"],
  ],
  ["wand",
    ["vigor","D+8","D+7"],
    ["default",
    "vigor wand D+8","vigor wand D+7"],
  ],
  ["staff",
    ["aether","dU"]
  ],
  ["crossbow",
    ["poison","dP"],
    ["fire","dF"],
    ["ice","dI"],
  ],
  ["sword",
    ["aether","dU+9","dU+8"],
    ["fire","D+7","dF"],
    ["ice","dI"],
    ["poison","dP","D"],
    ["vigor","D","dL+10","dL+9"],
    ["default",
     "aether sword dU+9","aether sword dU+8",
     "vigor sword dL","poison sword D"],
  ],
  ["shield",
    ["vigor","A"],
    ["poison","A"],
    ["ice","A"],
    ["aether","A"],
    ["fire","A"],
    ["default","compound"],
  ],
]
var special_weapons=["bardiche","blade",
"heavy hammer","repeater","arm"]
var for_melee_single_target=[
  "sword","hammer",
]
var use_mods_for_dmg=false
var aoe_treshold=4
var for_melee_aoe=["arm"]
var prefer_ranged=false
var prefer_ranged_foes=[]
var must_ranged_foes=["wasp","wasp nest",
"mosquito","huge mosquito"]
var for_ranged_single_target=["crossbow",
"repeating","wand",]
var enable_repeating_aac=true
var for_ranged_aoe=["wand","staff"]
var for_phys_immune=["wand","staff"]
var evade_shield="vigor shield ah"
var do_stunlock=[
  "poena","ceiling decorator","ice elemental"
]
var stunlock_weapons=[
  "aether hammer D","stone hammer+7"]
var unmaking_threshold=600
var debuff_bosses="always"
var debuff_non_bosses="smite"
var useable_abilities=["dash","bash",
"skeleton_arm","mask","blade","hammer",
"fire_talisman","bardiche","quarterstaff",
]
var hammer_frames=14
var bardiche_frames=22
var bardiche_dmg=52 * (7.6+2)
var use_potions_for_bosses=true
var fast_potion_bosses=[
  "xylo","mushrooms","pallas",
]
var low_hp_threshold=0.5
var use_shield_on_low_hp=true
var max_chill_stacks=6
var max_ignition_stacks=10
var cinderwisp_dmg=24
var smite_threshold=7
var smite_damage=85
var long_long_smite=true
var bfg_defeated_foes=[]
var pickpocket_quest=false
var arm_damage=44
var max_pickpocket_stacks=3
var mask_quest=false
var ui_show_cd=true
var ui_show_statuses=true
var ui_show_debug=true
var enable_tracing=true
var enable_hit_tracking=false
var speedrun=false
var harvesting=false
func progress()
  ?pickup.distance<9
    return equip_L("star")&equip_R("trisk")
  ?long_long_smite&smite_two_screens()
    return true
  ?healwalk()
    return true
  ?ff=explode
    return avoid_explosion()
  ?ll=caves
    check_stunlock()
  ?!ib&!at_boss
    free_mind=true
    return regular_fight()
  trace("boss")
  ?loc.stars>5&!pickpocket_quest
    dr("skeleton_arm")
  ?ll=rocky
    ?fi="acronian_scout"
      trace("miniboss")
      return fight_scout()
    return fight_dysan()
  :?ll=deadwood
    return fight_xyloalgia()
  :?ll=caves
    ?is(fi,"cool_bat")
      trace("miniboss")
      ?can_stunlock&
      ^do_stunlock.Contains("ceiling decorator")
        dr("mask")
        dr("blade")
        dr("bardiche")
        return stunlock()
      return auto_melee_miniboss()
    :
      return fight_bolesh()
  :?ll=fungus_forest_boss
    return fight_mushrooms()
  :?ll=mushroom&is(fi,"epic_snail")
    return dr("hammer")&
    ^dr("bash")&auto_melee_miniboss()
  :?ll=undead_crypt_boss
    return fight_pallas()
  :?ll=halls
    trace("miniboss")
    return ranged("aoe")
  :?ll=bronze_guardian
    return fight_bronze_guardian()
  :?ll=icy_ridge
    ?fi="ice_elemental_elite"
      ?!at_boss
        trace("miniboss")
        ?can_stunlock&
        ^do_stunlock.Contains("ice elemental")
          dr("mask")
          dr("blade")
          dr("hammer")
          return stunlock()
        :
        return auto_melee_miniboss()
      :
        return ranged("aoe")
    return fight_hrimnir()
  :?ll=temple&
  ^is(fi,"acronian_cultist")
    trace("miniboss")
    excluded_items="vigor"
    return auto_melee_miniboss()
  :?ll=nagaraja
    return fight_nagaraja()
  :
    dbg("Fighting unknown boss:"+fi)
    auto_melee_boss()
func healwalk()
  trace("healwalk")
  ?ff&(fd<23)
    ?hp<(maxhp/4)&item.potion!empty&
    ^(item.potion="healing"|
    ^item.potion="vampiric")
      activate potion
    return false
  :?harvesting&harvest.distance<3&
  ^_harvest()
    return true
  :?should_summon()
    return equip_L("trisk")&summon()
  :?can_qs_dash()
    return qs_dash()
  ae_shield()
  ?hp<maxhp
    return equip_L("ouroboros")
  return equip_L("trisk")
func _harvest()
  ?ll=Rocky
    return equip_L("shovel")
  :?ll=Deadwood
    equip_R("hatchet")
    ?harvest.distance<2
      use("hatchet")
    return true
  return false
func avoid_explosion()
  trace("explode")
  ?fd<5
    return try_evade()
  return ae_L("melee",false)&
  ^equip_R("dashing")
var scrolling=0
func smite_two_screens()
  var cd_ok=get_cd("blade")<=0
  ?cd_ok&fc>3
    scrolling++
    screen.Next()
  :
    scrolling=0
    screen.ResetOffset()
  ?foe_ehp<600&foe_ehp>0&cd_ok&
  ^fd<25&
  ^(fc>5|scrolling>15)
    return smite()
  return scrolling>0
func try_evade()
  trace("try_evade")
  ?can_mind
    set_lock("mind",2)
    return backflip()
  return hard_block()
func hard_block()
  trace("hard_block")
  return equip_L("quest")&
  ^equip_R(evade_shield)
func evade_at(t)
  trace("evade_at")
  ?ft>=t
    return try_evade()
  ?ft<t&get_cd("mind")<=(t-ft)
    ?ft>(t-bardiche_frames)
      dr("bardiche")
    ?ft>(t-hammer_frames)
      dr("hammer")
  return false
func soft_block()
  trace("soft_block")
  ?lock
    return false
  return equip_R(evade_shield)
func around(t)
  return ft>(t-2)&ft<(t+2)
func fight_scout()
  ?is(mask_quest,"prevent")
    equip_L("trisk")
    ?fs=32&ft>11&can_mask()
      return mask()
    ?get_cd("mask")>0&
    ^!foe_debuff("debuff_feeble")
      return loc.Leave()
    ?have_dashing
      equip_R("dashing")
    return
  :
    auto_melee_miniboss()
var last_state=0
var dysan_step=-1
func fight_dysan()
  dr("skeleton_arm")
  var delay=chill_delay()
  var hit=28
  ?loc.stars>10
    hit=22
  ?ff=phase1
    dr("blade")
    maybe_drink()
    ?(fs=33&ft=23)|
    ^(fs=32&around(hit+delay))
      return soft_block()
  :?ff=phase2
    ?smite_at_exact()|dash_closer()|
    ^(fs=33&use_abilities())
      return true
    trace("match_element")
    f=f_weapon("melee",false,
    ^weak_elem,"stats","false")
    ?f
      equip_L(f)
    ?fs=32&around(58+delay)
      return soft_block()
    f=false
    ?should_debuff()
      f=f_debuff_weapon("melee",
      ^false,debuff_elems[0],false)
    ?f
      return equip_R(f)
    return ae_R("melee",false)
  :?ff=phase3
    weak_elem="default"
    hit=20+math.Max(0,delay-6)
    ?fs=32&ft>hit&dysan_step=0
    ^&fd>10&can_hammer()
      return hammer()
    var resist=foe_buff("adaptive_defense")
    ?!(low_hp&is(resist,"vigor"))
      excluded_items=resist
    ?(last_state=32&fs=33)|
    ^(last_state=33&fs=2)|
    ^(last_state=2&fs=32)
      dysan_step++
    :?(fs!32)&(fs!33)&
    ^(fs!2)
      dysan_step=0
    last_state=fs
    ?fs=32&dysan_step>=6&
    ^evade_at(hit)
      dysan_step=0
      return
    :?fs=115&ft<=70&
    ^evade_at(70)
      return set_lock("boss",7)
    :?fs=32&(dysan_step<7)&
    ^around(48+delay)
      return soft_block()
  auto_melee_boss()
func fight_xyloalgia()
  dr("skeleton_arm")
  var stunlock_poena=loc.stars>5&
  ^can_stunlock&do_stunlock.Contains("poena")
  ?ff=phase1
    ?fast_potion_bosses.Contains("xylo")
      maybe_drink()
    ?stunlock_poena
      dr("mask")
      dr("hammer")
      dr("blade")
      dr("bardiche")
    ?fs=32&
    ^evade_at(31+chill_delay())
      return
    auto_melee_boss()
  :?ff=phase2
    ?unsummon()
      return
    excluded_items="dU"
    ?stunlock_poena
      stunlock()
    :?(disable_debuffs&fh>(fm/4))|
    ^(fs=32&ft=41)
      hard_block()
    :
      auto_melee_boss()
func fight_bolesh()
  ?fs=133&(around(11)|around(16))
    hard_block()
  :?fs=142&evade_at(60)
    return
  :?can_mind&
  ^!foe_buff("spider_buff_damage")
    maybe_drink()
    return use_abilities()|
    ^auto_melee_boss()
  :
    ?for_ranged_single_target.
    ^Contains("repeating")
      return equip_L("repeating")
    ranged("single")
func fight_mushrooms()
  var delay=chill_delay()
  ?ff=phase1
    ?fast_potion_bosses.Contains("mushrooms")
      maybe_drink()
    dr("skeleton_arm")
    ?fs=32&evade_at(46+delay)
      return
    :?fs=33&ft=fd+4
      return soft_block()
  :?ff=mushroom_boss_fat
    maybe_drink()
    ?fs=32&evade_at(26+delay)
      return
  auto_melee_boss()
func fight_pallas()
  dr("skeleton_arm")
  ?ff=phase1
    ?fast_potion_bosses.Contains("pallas")
      maybe_drink()
    ?fs=32&ft=68
      soft_block()
    :
      auto_melee_boss()
  :?ff=phase2
    maybe_drink()
    ?fc>2
      return ranged("aoe")
    auto_melee_boss()
func fight_bronze_guardian()
  ?fast_potion_bosses.Contains("bronze")
    maybe_drink()
  ?fs=32&ft=33&
  ^armor<foe.damage
    backflip()
  :?fs=33
    maybe_drink()
    auto_melee_boss()
  :
    ranged("single")
func fight_hrimnir()
  at_boss=true
  ?is_snowball_near()|
  ^(ib&fd<=6&
  ^fs=32&around(23+chill_delay()))
    ?useable_abilities.Contains("fire_talisman")
      equip_L("fire talisman")
    soft_block()
  :?draw.GetSymbol(56,13)="o"|ib&
  ^fs=133
    ?fc>1
      ranged("aoe")
    :
      ranged("single")
  :?ib&fs=132&ft>24
    set_lock("boss",2)
    auto_melee_boss()
  :?ib&fs=142&ft>83
    set_lock("boss",2)
    ?fc>1
      ranged("aoe")
    :
      ranged("single")
  :?ib&fc=1
    ?fa>0
      ?can_hammer()
        return hammer()
      return melee("dps")
    maybe_drink()
    auto_melee_boss()
  :?fd<8
    auto_melee()
  :
    auto_melee_boss()
var dists=[3,4]
func is_snowball_near()
  var x=screen.FromWorldX(pos.x)
  var y=screen.FromWorldZ(pos.z)
  for dist:dists
    ?draw.GetSymbol(x+dist,y-3)="("&
    ^draw.GetSymbol(x+dist+1,y-3)="_"&
    ^draw.GetSymbol(x+dist+2,y-3)=")"
      return true
  return false
var naga_step=0
func fight_nagaraja()
  ?fast_potion_bosses.Contains("naga")
    maybe_drink()
  var to_dodge=1
  ?loc.stars>10
    to_dodge=4
  :?loc.stars>5
    to_dodge=2
  ?is_poison_near()
    ?naga_step<=to_dodge
      ?can_mind&fd<12
        naga_step=0
        maybe_drink()
        auto_melee_boss()
      :
        naga_step++
        qs_dash()
    :
      evade_naga()
  :?fs=112&ft=59&
  ^fd<10
    evade_naga()
  :?naga_step<=to_dodge
    ?use_abilities()
      return
    ?fs=32|fs=33|
    ^((fs=101|fs=112)
    ^&ft<20)
      excluded_items="wand"
      f=f_dps_weapon("ranged",false,true)
      ?f
        return equip_L(f)
      excluded_items=false
    ranged("single")
  :
    maybe_drink()
    auto_melee_boss()
func evade_naga()
  ?can_mind
    naga_step=0
    return backflip()
  :
    return soft_block()
func is_poison_near()
  var x=screen.FromWorldX(pos.x)
  var y=screen.FromWorldZ(pos.z)
  return draw.GetSymbol(x+2,y-6)="("|
  ^(draw.GetSymbol(x+1,y-6)="(")
func regular_fight()
  trace("fight")
  ?must_ranged_foe()
    return ranged("single")
  ?ff=immune_to_ranged|ff=ranged
    return auto_melee()
  ?prefer_ranged_foe()&can_aoe&
  ^fd>melee_aoe_range
    return auto_ranged()
  ?prefer_ranged
    return (can_backflip()&backflip())|
    ^auto_ranged()
  return auto_melee()
func must_ranged_foe()
  for enemy:must_ranged_foes
    ?ff=enemy
      return true
func prefer_ranged_foe()
  for enemy:prefer_ranged_foes
    ?ff=enemy
      return true
func auto_melee()
  trace("auto_m")
  ?use_abilities()
    return true
  :?can_aoe&fd<=melee_aoe_range
    return melee("aoe")
  :?low_hp&use_shield_on_low_hp
    return melee("def")
  return melee("dps")
func melee(mode)
  trace("m_"+mode)
  ?dash_closer()
    return true
  var aoe=is(mode,"aoe")
  ?should_unmake()
    ?te_unmake("melee",aoe)
      trace("unmaking")
      mode="unmake"
      return approach()
  :?should_debuff()
    ?te_debuff("melee",aoe)
      trace("debuff")
      mode="debuff"
      return approach()
  ?is(mode,"def")
    ae_L("melee",false)
    ?use_shield_on_low_hp
      ae_shield()
    ae_R("melee",false)
  :
    ae_dps("melee",aoe)
  return approach()
func approach()
  var range=6
  ?f_type
    range=get(weapon_ranges,f_type)+1
  ?fd>range
    trace("approach")
    ?!is_2hander(f_type)
      ae_shield()
  return true
func dash_closer()
  trace("dash_closer")
  ?can_dash()
    return dash()
  :?should_qs_dash()
    return qs_dash()
  return false
func auto_ranged()
  trace("auto_r")
  ?use_abilities()
    return true
  :?can_aoe&for_ranged_aoe.Count()>0&
  ^fd<=ranged_range
    return ranged("aoe")
  :?for_ranged_single_target.Count()>0
    return ranged("single")
  return auto_melee()
func ranged(mode)
  trace("r_"+mode)
  var aoe=is(mode,"aoe")
  ?!aoe&must_ranged_foe()&
  ^for_ranged_single_target.
  ^Contains("repeating")
    return equip_L("repeating")
  ?should_unmake()
    ?te_unmake("melee",aoe)
      trace("unmaking")
      mode="unmake"
  :?should_debuff()
    ?te_debuff("ranged",aoe)
      return
  return ae_dps("ranged",aoe)
func auto_melee_boss()
  trace("auto_m_boss")
  ?use_abilities()|dash_closer()
    return true
  ?should_unmake()&
  ^te_unmake("melee",false)
    trace("unmaking")
    return approach()
  :?should_debuff()
    ?te_debuff("melee",false)
      trace("debuff")
      return approach()
  ae_dps("melee",false)
  return approach()
func auto_melee_miniboss()
  trace("auto_m_miniboss")
  return dr("blade")&
  ^dr("mask")&dr("cinderwisp")&
  ^auto_melee()
var can_stunlock=do_stunlock.Count()>0&
^stunlock_weapons.Count()>0
func check_stunlock()
  ?can_stunlock&fi!"cool_bat"&
  ^do_stunlock.Contains("ceiling decorator")
    dr("hammer")
func stunlock()
  trace("stunlock")
  dr("skeleton_arm")
  ?dash_closer()
    return true
  ?disable_debuffs
    return maybe_drink()&melee("dps")
  var stunned=foe_debuff("stun")
  ?(can_hammer()&fd<=17)|
  ^is(lock,"hammer")
    return hammer()
  :?(stunned>bardiche_frames+10)&
  ^should_bard()|is(lock,"bardiche")
    return bardiche()
  :?stunned>10
    ?should_cinder()
      return cinder()
    ?should_smite()
      return smite()
    ?should_mask()
      return mask()
  :?stunned<=3
    ?get_cd("bash")<=0&fd<10&
    ^foe_ehp>100&can_backflip()
      return backflip()
    ?can_qs_dash()
      return qs_dash()
  f=false
  for it:stunlock_weapons
    ?!f&!(it=excluded_items)&
    ^!(it=foe_elem)
      f=it
  ?f
    equip_L(f)
  :
    equip_L(stunlock_weapons[0])
  f=false
  ?should_unmake()
    f=f_unmake_weapon("melee",
    ^false,false)
  :?should_debuff()
    f=f_debuff_weapon("melee",
    ^false,debuff_elems[0],false)
  ?f
    equip_R(f)
  :
    ae_R("melee",false)
var _equip_L=false
func equip_L(it)
  _equip_L=it
  ?is_2hander(it)|is_2hander(f_type)
    return aac(it)
  equipL @it@
  return it
var _equip_R=false
func equip_R(it)
  _equip_R=it
  ?is_2hander(it)|is_2hander(f_type)
    return aac(it)
  equipR @it@
  return it
func is_2hander(ty)
  return ty&all_2h_types.Contains(ty)
func ae_L(range,aoe)
  trace("auto_eq_L")
  f=f_dps_weapon(range,aoe,true)
  ?f
    return equip_L(f)
  return false
func ae_R(range,aoe)
  trace("auto_eq_R")
  f=f_dps_weapon(range,aoe,false)
  ?f
    return equip_R(f)
  return ae_shield()
func ae_shield()
  trace("auto_eq_sh")
  f=false
  f_type=false
  ?low_hp
    f=get_item("shield","vigor","mod")
  ?!f
    f=get_item("shield",weak_elem,"stats")
  ?!f
    f=get_item("shield","default","")
  ?f
    return equip_R(f)
  return false
func ae_dps(range,aoe)
  trace("auto_eq_dps")
  f=f_dps_weapon(range,aoe,true)
  ?!f
    return false
  equip_L(f)
  ?is_2hander(f_type)
    return f
  f=f_dps_weapon(range,aoe,false)
  ?f
    return equip_R(f)
  return false
func f_dps_weapon(range,aoe,allow_2h)
  trace("f_dps_weapon")
  f=false
  ?low_hp
    f=f_weapon(range,aoe,
    ^"vigor","mod",allow_2h)
  ?!f
    f=f_weapon(range,aoe,
    ^weak_elem,"stats",allow_2h)
  trace(""+f)
  return f
func te_debuff(range,aoe)
  trace("try_eq_debuff")
  f=false
  for elem:debuff_elems
    f=f_debuff_weapon(range,aoe,
    ^elem,!_equip_L)
    ?f
      ?!_equip_L
        equip_L(f)
        ?is_2hander(f_type)
          return true
      :?!_equip_R
        ?low_hp
          return ae_R(range,aoe)
        return equip_R(f)
  ?_equip_L
    ?!_equip_R
      return ae_R(range,aoe)
  :
    trace("no_debuff_weapon")
    return false
  return true
func f_debuff_weapon(range,aoe,
^elem,allow_2h)
  trace("f_debuff_wep")
  f=f_weapon(range,aoe,
  ^elem,"mod",allow_2h)
  ?!f&range="melee"&
  ^!(ff=immune_to_ranged|ff=ranged)
    f=f_weapon("ranged",aoe,elem,
    ^"mod",allow_2h)
  return f
func te_unmake(range,aoe)
  trace("try_eq_unmake")
  ?prefer_ranged&fd>melee_range
    range="ranged"
  f=f_unmake_weapon(range,aoe,true)
  ?!f
    aoe=false
    f=f_unmake_weapon(range,aoe,true)
  ?!f
    trace("no_unmake_weapon")
    return false
  equip_L(f)
  ?is_2hander(f_type)
    return true
  ?low_hp
    return ae_R(range,aoe)
  f=f_unmake_weapon(range,aoe,false)
  ?f
    return equip_R(f)
  return ae_R()
func f_unmake_weapon(range,aoe,allow_2h)
  trace("f_unmake_wep")
  f=f_weapon(range,aoe,
  ^"aether","mod",allow_2h)
  ?!f&range="melee"
    f=f_weapon("ranged",aoe,
    ^"aether","mod",allow_2h)
  return f
func f_weapon(range,aoe,elem,
^purpose,allow_2h)
  f=false
  f_type=false
  var types=get_preferred_types(range,aoe)
  for ty:types
    ?allow_2h|!is_2hander(ty)
      ?!elem_types.Contains(ty)&
      ^!is(purpose,"mod")
        f_type=ty
        return ty
      f=get_item(ty,elem,purpose)
      ?!f&purpose="stats"
        ?use_mods_for_dmg
          f=get_item(ty,elem,"mod")
        ?!f
          f=get_item(ty,"default","")
      ?f
        f_type=ty
        return f
  return false
func get_preferred_types(range,aoe)
  ?phys_immune|
  ^(can_aoe&ff=magic_vulnerability)
    return for_phys_immune
  var types=for_melee_single_target
  ?is(range,"melee")
    ?fa>0
      ?aoe
        types=aa_aoe_types
      :
        types=aa_st_types
    :?aoe
      types=for_melee_aoe
  :
    ?aoe
      types=for_ranged_aoe
    :
      types=for_ranged_single_target
  return types
var excluded_items=""
func get_item(ty,elem,purpose)
  var h
  ?is(elem,"default")
    h=hash([ty,"default"])
  :
    h=hash([ty,elem,purpose])
  var items=try_get(all_items,h,false)
  ?!items|items.Count()=0
    return false
  for it:items
    ?(!_equip_L|!is(it,_equip_L))&
    ^(!excluded_items|!(it=excluded_items))
      return it
  return false
func should_unmake()
  return unmaking_threshold>0&!ib&
  ^fm>=unmaking_threshold
var only_dot=false
func can_debuff()
  only_dot=false
  ?disable_debuffs
    return false
  ?ib
    ?is(debuff_bosses,"never")
      return false
    ?is(debuff_bosses,"smite")&smite_stacks=0
      ?ll=Rocky
        return true
      ?cbd("debuff_dot")
        only_dot=true
        return true
      return false
  ?!ib&(is(debuff_non_bosses,"never")|
  ^is(debuff_non_bosses,"smite")&smite_stacks=0)
    return false
  return true
var disable_debuffs=false
var debuff_elems=[]
func should_debuff()
  ?!can_debuff()
    return false
  debuff_elems.Clear()
  ?cbd("debuff_dot")
    debuff_elems.Add("fire")
    ?only_dot
      return true
  ?cbd("debuff_chill")
    debuff_elems.Add("ice")
  ?cbd("debuff_damage")
    debuff_elems.Add("poison")
  var c=debuff_elems.Count()
  ?c>1&
  ^ff=debuff_elems[0]
    var move=debuff_elems[0]
    debuff_elems.Emplace(0,debuff_elems[c-1])
    debuff_elems.Emplace(c-1,move)
  return c>0
func cbd(debuff_uid)
  ?(ff="immune_to_"+debuff_uid)
    return false
  ?is(debuff_uid,"debuff_chill")
    var c=foe_debuff("debuff_chill")
    return c<max_chill_stacks
  return!foe_debuff(debuff_uid)
func get_cd(ability)
  return try_get(cooldowns,ability,9999)
func allowed(ability)
  return try_get(abilities,ability,false)
func set_lock(tag,timer)
  ?!lock|(is(tag,"boss")&lock_timer<timer)
    lock=tag
    lock_timer=timer
  return true
func cu(ability)
  ?lock
    return is(lock,ability)
  return allowed(ability)
func ca()
  return ai.enabled&item.CanActivate()
func use(ability)
  ?item.CanActivate(ability)
    activate R
  return true
func maybe_drink()
  ?use_potions_for_bosses&
  ^(item.potion="berserk"|item.potion="lucky")
  ^&(fd<=melee_range|
  ^ll=bronze_guardian&
  ^fast_potion_bosses.Contains("bronze")|
  ^ll=nagaraja&
  ^fast_potion_bosses.Contains("naga"))
    trace("drink")
    activate potion
  return true
func can_dash()
  return ca()&(cu("dash")|
  ^cu("bash")&!disable_debuffs)
  ^&fd>10&fd<16
func dash()
  trace("dash")
  ?!disable_debuffs&cu("bash")
    trace("bashing")
    return equip_R("bashing")
  :?cu("dash")
    trace("dashing")
    return equip_R("dashing")
  return false
func can_qs_dash()
  return cu("quarterstaff")&
  ^ca()
func should_qs_dash()
  return (prefer_ranged&fd>28)|
  ^(fd>16|
  ^fd<11&fd>5)&
  ^can_qs_dash()
func qs_dash()
  trace("qs_dash")
  equip_L("quarterstaff")
  return use("quarterstaff")
func can_backflip()
  return cu("mind")&free_mind&
  ^screen.FromWorldX(pos.x)>5
func backflip()
  trace("backflip")
  ae_shield()
  return equip_L("mind")
func can_slap()
  return cu("skeleton_arm")&
  ^!(fi="ice_pillar")&
  ^fd<7&!phys_immune&
  ^ca()
func should_slap()
  return can_slap()&(!pickpocket_quest|
  ^foe_ehp<=arm_damage)
func slap()
  trace("slap")
  ?pickpocket_stacks<max_pickpocket_stacks
    return aac("arm")
  equip_L("arm")
  return use("skeleton_arm")
func can_smite()
  return cu("blade")&
  ^fd<=20&(ff!pallas)&
  ^ca()
func should_smite()
  ?bfg_defeated_foes.Count()>0
    ?foe_ehp<=smite_damage&can_smite()
      for e:bfg_defeated_foes
        ?ff=e
          return false
      return true
    return false
  return (fc>=smite_threshold
  ^&foe_ehp<=smite_damage)&can_smite()
func smite()
  trace("smite")
  equip_L("blade")
  return use("blade")
func smite_at_exact()
  dr("blade")
  ?fh<=smite_damage&get_cd("blade")<=0
    return smite()
  return false
func can_hammer()
  return cu("hammer")&!phys_immune&
  ^fd<=21&ca()
func should_hammer()
  ?!can_hammer()
    return false
  return is(lock,"hammer")|
  ^(ib|can_aoe&fd<15)
  ^&(fa>0|ff=spawner|
  ^can_debuff()&
  ^!(ff=immune_to_stun))
func hammer()
  trace("hammer")
  set_lock("hammer",hammer_frames)
  equip_L("heavy hammer")
  return use("hammer")
func can_bard()
  return cu("bardiche")&
  ^fd<=9&!phys_immune&
  ^ca()
func should_bard()
  return is(lock,"bardiche")|ib&
  ^!aspd_down&(foe_ehp>=(bardiche_dmg * 2)|
  ^foe_ehp<bardiche_dmg)&can_bard()
func bardiche()
  trace("bardiche")
  set_lock("bardiche",bardiche_frames)
  equip_L("bardiche")
  return use("bardiche")
func can_mask()
  return cu("mask")&
  ^(ff!nagaraja)&ca()
func should_mask()
  return can_mask()&(is(mask_quest,"simple")|
  ^ib&fd<=melee_range)&
  ^!disable_debuffs&can_debuff()
func mask()
  trace("mask")
  equip_R("mask")
  return use("mask")
func can_summon()
  return (summon.GetId()!"cinderwisp"|
  ^lock="fire_talisman")&
  ^cu("fire_talisman")&ca()
func should_summon()
  return (fi!"poena")&can_summon()
var fire_frames=20
func summon()
  trace("summon")
  set_lock("fire_talisman",fire_frames)
  equip_R("fire talisman")
  return use("fire_talisman")
func unsummon()
  trace("unsummon")
  ?summon.count>0
    equip_R("fire talisman")
    return use("fire_talisman")
  return false
func can_cinder()
  return summon.GetId()="cinderwisp"&
  ^cu("cinderwisp")&ca()
func should_cinder()
  return ib&can_cinder()&
  ^(summon.GetVar("ignition",0)>=
  ^max_ignition_stacks|foe_ehp<=
  ^max_ignition_stacks * cinderwisp_dmg)
func cinder()
  trace("cinder")
  activate cinderwisp
  return true
func use_abilities()
  trace("use_abilities")
  ?is(mask_quest,"simple")&can_mask()
    return mask()
  ?pickpocket_quest&can_slap()
    return slap()
  ?should_cinder()
    return cinder()
  ?should_mask()
    return mask()
  ?should_hammer()
    return hammer()
  ?should_smite()
    return smite()
  ?should_bard()
    return bardiche()
  ?should_slap()
    return slap()
  return false
var aac_tick=1
func aac(weapon)
  _equip_L=weapon
  equip @weapon@
  ?!ai.idle
    return true
  ?is(lock,"hammer")|
  ^is(lock,"bardiche")
    return true
  ?weapon="repeating"
    ?!enable_repeating_aac
      return weapon
    aac_tick--
    ?aac_tick=0
      aac_tick=2
      equip blade
      equip @weapon@
    return weapon
  ?weapon="wand"|weapon="staff"
    return true
  equip wand
  equip @weapon@
  return weapon
var elem_types=["sword","big sword","wand",
"shield","crossbow","staff","hammer"]
var all_2h_types=["bardiche","arm","blade",
"heavy hammer","repeating","staff",
"quarterstaff","shovel","crossbow"]
var ranged_2h_types=["repeating","crossbow",
"staff"]
var al_elems=["aether","fire","ice",
"poison","vigor"]
var aa_st_types=[]
var aa_aoe_types=[]
var all_items=[]
func _prepare_items()
  for item_type:elemental_items
    ?item_type.Count()>1
      var ty=item_type[0]
      for i=1 .. item_type.Count()-1
        var suffixes=item_type[i]
        ?suffixes.Count()>1
          _categorize_item(ty,suffixes)
  for a:[
  ^[for_melee_single_target,aa_st_types],
  ^[for_melee_aoe,aa_aoe_types]]
    var source=a[0]
    var dest=a[1]
    ?source.Contains("heavy hammer")
      dest.Add("heavy hammer")
    ?source.Contains("hammer")
      dest.Add("hammer")
    for w:source
      ?!(w="hammer")
        dest.Add(w)
func _categorize_item(ty,suffixes)
  var elem=suffixes[0]
  for j=1 .. suffixes.Count()-1
    var suffix=suffixes[j]
    ?is(elem,"default")
      add_item(hash([ty,"default"]),suffix)
    :
      var purpose=get_purpose(suffix)
      var full_name=string.Join(" ",
      ^[elem,ty,suffix])
      var h=hash([ty,elem,purpose])
      add_item(h,full_name)
func hash(parts)
  return string.Join("-",parts)
var elem_mods=["f","F","i","I",
"p","P","h","L","u","U"]
func get_purpose(suffix)
  ?string.Size(suffix)>1&
  ^elem_mods.Contains(string.Sub(suffix,1,1))
    return "mod"
  return "stats"
func add_item(hash,_name)
  var idx=all_items.IndexOf(hash)
  var arr
  ?idx=-1
    arr=[_name]
    all_items.Add(hash)
    all_items.Add(arr)
  :
    arr=all_items[idx+1]
    ?!arr.Contains(_name)
      arr.Add(_name)
var all_statuses=[
  ["berserk","Berserk","+"],
  ["smite","Smite","+"],
  ["pick_pocket","Pickpocket","+"],
  ["vampiric","Vamp","+"],
  ["lucky_crit","CritUp","+"],
  ["lucky_mult","CritMultUp","+"],
  ["invisibility","Invis","+"],
  ["debuff_damage","DmgDown","-"],
  ["debuff_duration_damage","DmgDown","-"],
  ["spider_debuff_damage","DmgDown","-"],
  ["dysangelos_debuff_damage","DmgDown","-"],
  ["puff_debuff_damage","DmgDown","-"],
  ["debuff_yeti_chill","AtkSlow","-"],
  ["debuff_attack_speed","AtkSlow","-"],
  ["debuff_move_speed","MoveSlow","-"],
  ["debuff_chill","Chill","-"],
  ["stun","Stun","-"],
  ["pallas_phase2_debuff","RangeDown","-"],
  ["debuff_dot","FireDoT","-"],
  ["ignition","Ignition","-"]
]
var status_uids=[]
func _prepare_statuses()
  for info:all_statuses
    status_uids.Add(info[0])
var my_buffs=[]
var my_debuffs=[]
var unk_effects=[]
var pickpocket_stacks=0
var smite_stacks=0
var stunned=false
var aspd_down=false
func _update_my_effects()
  my_buffs.Clear()
  my_debuffs.Clear()
  pickpocket_stacks=0
  smite_stacks=0
  stunned=false
  aspd_down=false
  for s:[buffs.string,debuffs.string]
    var splt=string.Split(s,":",",",false)
    var arr_size=splt.Count()
    ?arr_size>0
      for i=1 .. arr_size-3
        _parse_status(splt,i)
        i=i+3
var ignore_statuses=[
  "quarterstaff_buff_attack_speed",
  "quarterstaff_buff_stun",
  "bardiche_buff_aoe_chance",
  "bardiche_buff_crit_chance",
  "bardiche_buff_crit_mult",
  "bardiche_buff_move_speed",
]
var aspd_debuffs=[
  "debuff_chill","debuff_attack_speed",
  "debuff_yeti_chill",
]
var buff_template="{0}({1}) {2}"
func _parse_status(splt,i)
  var uid=splt[i]
  var idx=status_uids.IndexOf(uid)
  ?idx=-1
    ?!ignore_statuses.Contains(uid)&
    ^!unk_effects.Contains(uid)
      unk_effects.Add(uid)
    return
  var info=all_statuses[idx]
  var num=int.Parse(splt[i+1])
  var dur=int.Parse(splt[i+2])
  var _name=string.Format(buff_template,
  ^info[1],num,time.FormatDigital(dur))
  ?info[2]="+"
    my_buffs.Add(_name)
  :
    my_debuffs.Add(_name)
  ?is(uid,"stun")
    stunned=true
  :?is(uid,"pick_pocket")
    pickpocket_stacks=num
  :?is(uid,"smite")
    smite_stacks=num
  :?aspd_debuffs.Contains(uid)
    aspd_down=true
func foe_buff(uid)
  return get_foe_effect(foe.buffs.string,uid)
func foe_debuff(uid)
  return get_foe_effect(foe.debuffs.string,uid)
func chill_delay()
  return 7 * foe_debuff("debuff_chill")
func get_foe_effect(str,uid)
  ?!(str=uid)
    return 0
  ?is(uid,"adaptive_defense")
    for s:string.Split(str,":",",",false)
      ?s="adaptive_defense"
        return string.Split(s,"_",false)[2]
  var splt=string.Split(str,":",",",false)
  var i=splt.IndexOf(uid)
  ?is(uid,"stun")
    return int.Parse(splt[i+2])
  return int.Parse(splt[i+1])
var foe_elem="default"
func get_foe_weak_elem()
  ?ff=aether
    foe_elem="aether"
    return "vigor"
  ?ff=fire
    foe_elem="fire"
    return "aether"
  ?ff=ice
    foe_elem="ice"
    return "fire"
  ?ff=poison
    foe_elem="poison"
    return "ice"
  ?ff=vigor
    foe_elem="vigor"
    return "poison"
  foe_elem="default"
  return "default"
var all_abilities=[
  "mind","hammer","bardiche","blade",
  "skeleton_arm","mask","fire_talisman",
  "cinderwisp","quarterstaff","dash","bash",
]
var move_r=["quarterstaff","bash","dash"]
var cooldowns=["mind",0]
var abilities=["mind",false]
func _prepare_abilities()
  for r:useable_abilities
    ?!all_abilities.Contains(r)
      panic("Unknown ability:"+r)
    ?!abilities.Contains(r)
      set(cooldowns,r,0)
      set(abilities,r,false)
var can_mind=false
func _update_abilities()
  for i=0 .. abilities.Count()-2
    var r=abilities[i]
    var cd=item.GetCooldown(r)
    set(cooldowns,r,cd)
    set(abilities,r,cd<=0)
    ?is(r,"mind")
      can_mind=cd<=0
    i++
func dr(ability)
  ?useable_abilities.Contains(ability)
    set(abilities,ability,false)
  return true
func get(dict,k)
  var idx=dict.IndexOf(k)
  ?idx=-1
    panic("Bad dict key:k="+k)
  return dict[idx+1]
func try_get(dict,k,default)
  var idx=dict.IndexOf(k)
  ?idx=-1
    return default
  return dict[idx+1]
func set(dict,k,v)
  var idx=dict.IndexOf(k)
  ?idx=-1
    dict.Add(k)
    dict.Add(v)
  :
    dict[idx+1]=v
  return true
func is(s1,s2)
  ?!s1|!s2
    return false
  return string.Equals(s1,s2)
var weapon_ranges=["sword",5,"hammer",5,
"quarterstaff",5,"big sword",6,"arm",6,
"heavy hammer",7,"bardiche",9,"staff",10,
"blade",11,"wand",20,"heavy crossbow",22,
"repeating",22,"crossbow",22]
var melee_range=5
var melee_aoe_range=5
var ranged_range=20
func _prepare_ranges()
  var a=for_melee_single_target
  ?a.Count()>0
    melee_range=get(weapon_ranges,a[0])+1
  a=for_melee_aoe
  ?a.Count()>0
    melee_aoe_range=get(weapon_ranges,a[0])+1
  a=for_ranged_aoe
  ?a.Count()>0
    ranged_range=get(weapon_ranges,a[0])+1
func _update_lock()
  ?stunned
    set_lock("stun",0)
  :?lock
    ?is(lock,"stun")|lock_timer<=0
      lock=false
    :
      lock_timer--
  :
    lock_timer=0
var at_boss=false
var low_hp=false
var can_aoe=false
var f=false
var f_type=false
var weak_elem="default"
var lock=false
var lock_timer=false
var free_mind=false
var foe_ehp=0
var ib=false
var phys_immune=false
var ff=foe
var ll=loc
var fa=0
var fc=0
var fd=0
var fh=0
var fi=0
var fm=0
var fs=0
var ft=0
var tt=0
func _update_game_state()
  ff=foe
  ll=loc
  fa=foe.armor
  fc=foe.count
  fd=foe.distance
  fh=foe.hp
  fi=foe.id
  fm=foe.maxhp
  fs=foe.state
  ft=foe.time
  tt=totaltime
  _equip_L=false
  _equip_R=false
  f=false
  f_type=false
  excluded_items=false
  weak_elem=get_foe_weak_elem()
  disable_debuffs=foe.buffs.count>0&
  ^(foe_buff("poena_mirror")|
  ^foe_buff("buff_protection"))
  low_hp=hp<(maxhp * low_hp_threshold)
  foe_ehp=fa+fh
  ib=(ff=boss)
  phys_immune=(ff=immune_to_physical)
  can_aoe=(fc>=aoe_threshold)
  traces.Clear()
func prelude()
  ?speedrun&(tt>loc.averageTime)
    loc.Leave()
  _update_game_state()
  _update_my_effects()
  _update_abilities()
  _update_lock()
func panic(e)
  >`20,5,#red,@e@
  loc.Pause()
var errors=[]
func dbg(e)
  ?ui_show_debug
    ?!errors.Contains(e)
      errors.Add(e)
var traces=[]
func trace(tag)
  ?enable_tracing&ui_show_debug
    traces.Add(tag)
var gray="#7A8F93"
func draw_ui()
  var x=1
  var y=23
  ?ui_show_debug
    y=21
  ?ui_show_statuses
    ?ui_show_debug
      >`@x@,@y@,#F008B2,
      ^? @string.Join(",",unk_effects)@
      y++
    >`@x@,@y@,#FE6800,
    ^- @string.Join(",",my_debuffs)@
    >`@x@,@y+1@,#00A300,
    ^+ @string.Join(",",my_buffs)@
    ?!ui_show_debug
      >`@x@,@y+2@,@gray@,@loc.stars@*
      ^ @time.FormatDigital(tt)@
      ^/@time.FormatDigital(loc.averageTime)@
  ?ui_show_cd
    y=6
    for r:all_abilities
      ?!move_r.Contains(r)&r!"fire_talisman"&
      ^abilities.Contains(r)
        var cd=get_cd(r)
        ?is(r,"skeleton_arm")
          r="arm"
        :?is(r,"cinderwisp")
          r="cinder"
        >`@x@,@y@,#@colorize(cd)@,
        ^@string.Capitalize(r)@
        ^ @time.FormatDigital(cd)@
        y++
    >`@x@,@y@,
    ^#@colorize(get_cd("quarterstaff"))@,QS
    ?abilities.Contains("bash")
      >`@x+2@,@y@,@gray@,/
      >`@x+3@,@y@,
      ^#@colorize(get_cd("bash"))@,B
    ?abilities.Contains("dash")
      >`@x+4@,@y@,@gray@,/
      >`@x+5@,@y@,
      ^#@colorize(get_cd("dash"))@,D
  ?ui_show_debug
    x=1
    y=15
    ?ff
      ?string.Size(foe.buffs.string)>0
        >`@x@,@y@,@gray@,@foe.buffs.string@
      >`@x@,@y+1@,@gray@,id: @fi@
      >`@x@,@y+2@,@gray@,S: @fs@,
      ^ T: @ft@
      var c=foe_debuff("debuff_chill")
      >`@x@,@y+3@,@gray@,Chill: @c@/@c*7@
      >`@x@,@y+4@,@gray@,Dist: @fd@
      >`@x@,@y+5@,@gray@,Count: @fc@
    y=24
    var frame_time=0
    for ft:frame_times
      frame_time=frame_time+ft
    var info="ms:"+frame_time/
    ^frame_times.Count()
    ?lock
      info=info+",L:"+lock+
      ^" ("+lock_timer+")"
    >`@x@,@y@,#blue,@info@
    >`@x@,@y+1@,@gray@,@loc.stars@*
    ^ @time.FormatDigital(tt)@
    ^/@time.FormatDigital(loc.averageTime)@
    ^ @ff@
    var _weapon=" (L)"
    x=screen.w-10
    y=23
    ?_equip_L
      x=x-string.Size(_equip_L)
      _weapon=_equip_L+_weapon
    >`@x@,@y@,#blue,@_weapon@
    _weapon=" (R)"
    x=screen.w-10
    ?_equip_R
      x=x-string.Size(_equip_R)
      _weapon=_equip_R+_weapon
    >`@x@,@y+1@,#blue,@_weapon@
    var ey=2
    for err:errors
      >`20,@ey@,#red,@err@
      ey++
    ?enable_tracing
      var tr=string.Break(string.Join(
      ^",",traces),50)
      ey=2
      for t:tr
        >`20,@ey@,#blue,@t@
        ey++
func colorize(cd)
  ?cd>0
    return "red"
  return "green"
var tracked=[-1,-1,-1,-1,-1,"",""]
func track_hits()
  var hp_diff=tracked[0]-hp
  ?hp_diff>0
    var y=14
    var x=55
    >`@x@,@y@,#red,Got hit!
    >`@x@,@y+1@,Last frame info:
    >`@x@,@y+2@,hp=@tracked[0]@ (diff=
    ^@hp_diff@)
    >`@x@,@y+3@,state=@tracked[1]@
    >`@x@,@y+4@,time=@tracked[2]@
    >`@x@,@y+5@,chill=@tracked[3]@
    >`@x@,@y+6@,dist=@tracked[4]@
    >`@x@,@y+7@,L=@tracked[5]@
    >`@x@,@y+8@,R=@tracked[6]@
    loc.Pause()
  tracked[0]=hp
  tracked[1]=fs
  tracked[2]=ft
  var chill=foe_debuff("debuff_chill")
  tracked[3]=chill+"/"+7*chill
  tracked[4]=fd
  tracked[5]=_equip_L
  tracked[6]=_equip_R
func brew_pot()
  ?ll=Mushroom|ll=mine|
  ^ll=halls|ll=rocky|ll=deadwood|
  ^ll=icy_ridge
    brew wood+bronze
  :?ll=caves|ll=temple
    brew stone+bronze
func reset_loop()
  lock=false
  lock_timer=0
  unk_effects.Clear()
  enable_hit_tracking=false
  at_boss=false
  naga_step=0
  last_state=0
  dysan_step=-1
var start_t=0
var frame_times=[]
var index=0
start_t=time.ms
?loc.begin
  equip_L("quest")
  ae_shield()
  _prepare_statuses()
  _prepare_items()
  _prepare_ranges()
  _prepare_abilities()
  ?use_potions_for_bosses
    brew_pot()
  :?item.potion=empty
    brew tar+bronze
:?loc.loop
  ?use_potions_for_bosses
    brew_pot()
  reset_loop()
prelude()
?ai.enabled&!ai.paused
  progress()
:
  healwalk()
draw_ui()
?enable_hit_tracking
  track_hits()
?frame_times.Count()<30
  frame_times.Add(time.ms-start_t)
:
  frame_times.Emplace(index,time.ms-start_t)
  index++
  ?index=frame_times.Count()
    index=0